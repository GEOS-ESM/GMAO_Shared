function lplt1 (args)

'numargs  'args
 numargs = result

obsnam  = 'NULL'
control = 'NULL'
numrc   = 'NULL'

        num = 0
while ( num < numargs )
        num = num + 1

if( subwrd(args,num) = '-EXPID'   ) ; expid    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-EXPORT'  ) ; EXPORT   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ALIAS'   ) ; alias    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-FILE'    ) ; file     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-NNUM'    ) ; nnum     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-QDESC'   ) ; qdesc    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-OUTPUT'  ) ; output   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-SEASON'  ) ; season   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ONAME'   ) ; obsnam   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-COLOR'   ) ; color    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-FIELD'   ) ; field    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-CONTROL' ) ; control  = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-NUMRC'   ) ; numrc    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-CLIM'    ) ; climflag = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-BEGDATE' ) ; begdate  = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ENDDATE' ) ; enddate  = subwrd(args,num+1) ; endif

endwhile


* Echo Input Parameters
* ---------------------

say '-EXPID   'expid
say '-EXPORT  'EXPORT
say '-ALIAS   'alias
say '-FIELD   'field
say '-CONTROL 'control
say '-COLOR   'color
say '-QFILE   'file
say '-NNUM    'nnum
say '-QDESC   'qdesc
say '-OUTPUT  'output
say '-SEASON  'season
say '-NUMRC   'numrc
say '-ONAME   'obsnam
say '-CLIM    'climflag
say ''


* Get Plotting Values from Resource File
* --------------------------------------
'run getenv "GEOSUTIL"'
         geosutil = result
PLOTRC = geosutil'/plots/grads_util/plot.rc'
 
if( EXPORT = "U"   )
    title = "Zonal Mean U-Wind (m/sec)"
    zlog  = off
    ptop  = 10.0
    lats  = "-30 0 30"
endif

if( EXPORT = "V"   )
    title = "Zonal Mean V-Wind (m/sec)"
    zlog  = off
    ptop  = 10.0
    lats  = "-30 0 30"
endif

if( EXPORT = "T"   )
    title = "Zonal Mean Temperature (K)"
    zlog  = on
    ptop  = 0.1
    lats  = "-85 0 85"
endif

if( EXPORT = "Q"   )
    title = "Zonal Mean Specific Humidity (g/kg)"
    zlog  = off
    ptop  = 300
    lats  = "-60 0 60"
endif

if( EXPORT = "RH2" )
    title = "Zonal Mean Relative Humidity (%)"
    zlog  = off
    ptop  = 10
    lats  = "-85 0 85"
endif

if( obsnam = 'NULL' )
    cthick = 8
else
    cthick = 3
endif

* Make Mean Plot
* --------------
'set dfile 'file
'set xlopts 1 3 .08'
'set ylopts 1 3 .11'
'set y 1'
'getinfo lat'
         latmin = result
'getinfo ydim'
         ydim = result
'set y  'ydim
'getinfo lat'
         latmax = result

'set vpage 0 4.3 0 8'
'set grads off'

if( EXPORT = 'T' | EXPORT = 'Q' )
       lat = subwrd(lats,1)
else
    if( control != 'NULL' )
        if( EXPORT = 'U' )
           'set lev 200'
           'set lat 'latmin' -20'
           'setmax_lat 'field
        endif
        if( EXPORT = 'V' )
           'set lev 950'
           'set lat -60 -30'
           'setmax_lat 'field
        endif
        if( EXPORT = 'RH2' )
           'set lev 500'
           'set lat -60 0'
           'setmin_lat 'field
        endif
        'getinfo lat'
                 lat = result
        'run unsetenv "LAT1" '
        'run   setenv "LAT1" 'lat
    else
        'run   getenv "LAT1"'
                       lat = result
    endif
endif
'set lat 'lat

'set lev 1000 'ptop
'set zlog 'zlog
if( control != 'NULL' )
'!remove   axlim1 '
'set_axlim axlim1 'control
endif
'set_axlim axlim1 '
 if( EXPORT = "T" ) ; 'setlevs' ; endif
'set cstyle 1'
'set cthick  'cthick
'set cmark  0'
'set ccolor  'color
'd 'field
if( control != 'NULL' )
'draw ylab Pressure (mb)'
'draw xlab Lat: 'lat
endif

'set vpage 3.35 7.65 0 8'
'set grads off'

if( EXPORT = 'T' | EXPORT = 'U' )
       lat = subwrd(lats,2)
else
    if( control != 'NULL' )
        if( EXPORT = 'Q' )
           'set lev 850'
           'set lat -30 30'
           'setmax_lat 'field
        endif
        if( EXPORT = 'V' )
           'set lev 200'
           'set lat -30 30'
           'setmax_lat 'field
        endif
        if( EXPORT = 'RH2' )
           'set lev 700'
           'set lat -10 10'
           'setmax_lat 'field
        endif
       'getinfo lat'
                lat = result
        'run unsetenv "LAT2" '
        'run   setenv "LAT2" 'lat
    else
        'run   getenv "LAT2"'
                       lat = result
    endif
endif
'set lat 'lat

'set lev 1000 'ptop
'set zlog 'zlog
if( control != 'NULL' )
'!remove   axlim2 '
'set_axlim axlim2 'control
endif
'set_axlim axlim2 '
 if( EXPORT = "T" ) ; 'setlevs' ; endif
'set cstyle 1'
'set cthick  'cthick
'set cmark  0'
'set ccolor  'color
'd 'field
if( control != 'NULL' )
'draw xlab Lat: 'lat
endif

'set vpage 6.7 11 0 8'
'set grads off'

if( EXPORT = 'T' | EXPORT = 'Q' )
       lat = subwrd(lats,3)
else
    if( control != 'NULL' )
        if( EXPORT = 'U' )
           'set lev 200'
           'set lat 20 'latmax
           'setmax_lat 'field
        endif
        if( EXPORT = 'V' )
           'set lev 950'
           'set lat 30 60'
           'setmax_lat 'field
        endif
        if( EXPORT = 'RH2' )
           'set lev 500'
           'set lat 0 60'
           'setmin_lat 'field
        endif
        'getinfo lat'
                 lat = result
        'run unsetenv "LAT3" '
        'run   setenv "LAT3" 'lat
    else
        'run   getenv "LAT3"'
                       lat = result
    endif
endif
'set lat 'lat

'set lev 1000 'ptop
'set zlog 'zlog
if( control != 'NULL' )
'!remove   axlim3 '
'set_axlim axlim3 'control
endif
'set_axlim axlim3 '
 if( EXPORT = "T" ) ; 'setlevs' ; endif
'set cstyle 1'
'set cthick  'cthick
'set cmark  0'
'set ccolor  'color
'd 'field
if( control != 'NULL' )
'draw xlab Lat: 'lat
endif


* Draw Labels for Each Experiment
* -------------------------------
'set vpage off'

if( numrc != 'NULL' )
 yloc = 8.3-0.15*(numrc-1)
 xbeg = 8.2
 xend = 8.7
 xloc =  xend+0.3
 xmid = (xbeg+xend)/2
  'set string 1 l 5'
  'set strsiz .08'
  'set  line 'color' 1 6'
  'draw line 'xbeg' 'yloc' 'xend' 'yloc
  'draw mark 3 'xmid' 'yloc' 0.08'
  'draw string 'xloc' 'yloc' 'obsnam' ('nnum') 'climflag

else

'set string 1 c 6'
'set strsiz .12'
'draw string 5.7  8.2   'title

                date = getdate (begdate)
bmnthm = subwrd(date,1)
byearm = subwrd(date,2)
                date = getdate (enddate)
emnthm = subwrd(date,1)
eyearm = subwrd(date,2)

'set string 1 l 6'
'set strsiz .13'
'draw string 0.85 8.00 'expid'  'season' ('nnum')'
'set strsiz .11'
'draw string 0.85 7.75 'qdesc

'set string 1 c 4'
'set strsiz .08'
'draw string 5.7  7.57 Begin Date: 'bmnthm' 'byearm
'draw string 5.7  7.42   End Date: 'emnthm' 'eyearm

endif

return

function getdate (date,month,year)
       num = 1
       bit = substr(date,num,1)
while( bit != '' )
       num = num+1
       bit = substr(date,num,1)
endwhile
       loc = num-7
     month = substr(date,loc  ,3)
      year = substr(date,loc+3,4)
return month' 'year
