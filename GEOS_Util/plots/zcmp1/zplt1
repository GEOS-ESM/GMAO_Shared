function zplt (args)

'numargs  'args
 numargs = result

        num = 0
while ( num < numargs )
        num = num + 1

if( subwrd(args,num) = '-EXPID'   ) ; expid    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-EXPORT'  ) ; EXPORT   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ALIAS'   ) ; alias    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-QFILE'   ) ; qfile    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-OFILE'   ) ; ofile    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ONAME'   ) ; obsnam   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-OBDATE'  ) ; begdateo = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-OEDATE'  ) ; enddateo = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-NMOD'    ) ; nmod     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-NOBS'    ) ; nobs     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-FACM'    ) ; facm     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-FACO'    ) ; faco     = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-QDESC'   ) ; qdesc    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-ODESC'   ) ; odesc    = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-OUTPUT'  ) ; output   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-SEASON'  ) ; season   = subwrd(args,num+1) ; endif
if( subwrd(args,num) = '-PTOP'    ) ; ptop     = subwrd(args,num+1) ; endif

endwhile


* Check for Contour Level Type
* ----------------------------
'run getenv "LEVTYPE"'
         LEVTYPE = result


say '-EXPID 'expid
say '-EXPORT 'EXPORT
say '-QFILE 'qfile
say '-OFILE 'ofile
say '-ONAME 'obsnam
say '-OBDATE 'begdateo
say '-OEDATE 'enddateo
say '-NMOD 'nmod
say '-FACM 'facm
say '-FACO 'faco
say '-QDESC 'qdesc
say '-ODESC 'odesc
say '-OUTPUT 'output
say '-SEASON 'season

* Get Dates for Plots
* -------------------
'run getenv "BEGDATE"'
         begdate  = result
'run getenv "ENDDATE"'
         enddate  = result
if( begdate = "NULL" )
   'set dfile 'qfile
   'set t    '1
   'getinfo date'
         begdate = result
endif
if( enddate = "NULL" )
   'set dfile 'qfile
   'getinfo tdim'
            tdim     = result
   'set t  'tdim
   'getinfo date'
         enddate = result
endif


'set gxout shaded'

* Get Plotting Values from Resource File
* --------------------------------------
'run getenv "GEOSUTIL"'
         geosutil = result
PLOTRC = geosutil'/plots/grads_util/plot.rc'
 
if( EXPORT = "U"   )
    oname = "/zonal_"obsnam"_uwnd"
   'getresource 'PLOTRC' U_DYN_TITLE'           ; title = result
    cmid  =  0 
    clevs = "0"
    ccols = "84 0"
    cint  = 5
    dint  = 5
endif
if( EXPORT = "V"   )
    oname = "/zonal_"obsnam"_vwnd"
   'getresource 'PLOTRC' V_DYN_TITLE'           ; title = result
    cmid  =  0 
    clevs = "0"
    ccols = "84 0"
    cint  = 0.5
    dint  = 0.5
endif
if( EXPORT = "T"   )
    oname = "/zonal_"obsnam"_tmpu"
   'getresource 'PLOTRC' T_DYN_TITLE'           ; title = result
    cmid  =  220
    clevs = "210 230"
    ccols = "85 0 84"
    cint  = 5
    dint  = 2
endif
if( EXPORT = "RH2"   )
    oname = "/zonal_"obsnam"_rh"
   'getresource 'PLOTRC' RH2_MOIST_TITLE'           ; title = result
    cmid  =  50
    clevs = "30 70"
    ccols = "84 0 85"
    cint  = 5
    dint  = 5
endif

* Make Mean Plot
* --------------
'set vpage off'
'set grid  off'
'set clab  on'
'set xlopts 1 3 .11'
'set ylopts 1 3 .11'
'rgbset'
'set rgb 84 204 204 204'
'set rgb 85 137 137 137'

    n = -30
 val  = n*cint + cmid
cvals = val
       n = n+1
while (n<=30 )
 val  = n*cint + cmid
cvals = cvals' 'val
       n = n+1
endwhile

'set dfile 'qfile
'set lat -90 90'
'set lon 0'
'set lev 1000 'ptop
'set t 1'
'set vpage 0.20  5.3 4.1 7.8'
'set zlog on'
'set ylevs 1000 700 500 300 200 100 70 50 30 20 10 7 5 3 2 1 0.4 0.2 0.1 0.04 0.02 .01 .004 .002 .001'
'set grads off'
'set clopts -1 -1 .13'
'set xlopts 1 4 .2'
'set ylopts 1 4 .2'
'set gxout shaded'
'set clevs 'clevs
'set ccols 'ccols
'd modz'
'set gxout contour'
'set ccolor 1'
'set clevs 'cvals
'd modz'
'set strsiz .15'
'set string 1 c'
'draw ylab Pressure (mb)'


'set dfile 'ofile
'set lat -90 90'
'set lon 0'
'set lev 1000 'ptop
'set t 1'
'set vpage 5.20 10.30 4.1 7.8'
'set zlog on'
'set ylevs 1000 700 500 300 200 100 70 50 30 20 10 7 5 3 2 1 0.4 0.2 0.1 0.04 0.02 .01 .004 .002 .001'
'set grads off'
'set clopts -1 -1 .13'
'set xlopts 1 4 .2'
'set ylopts 1 4 .2'
'set gxout shaded'
'set clevs 'clevs
'set ccols 'ccols
'd obsz'
'set gxout contour'
'set ccolor 1'
'set clevs 'cvals
'd obsz'
'set strsiz .15'
'set string 1 c'
'draw ylab Pressure (mb)'


    n = -30
 val  = n*dint
cvals = val
       n = n+1
while (n<=30 )
 val  = n*dint
cvals = cvals' 'val
       n = n+1
endwhile

'set dfile 'qfile
'set lat -90 90'
'set lon 0'
'set lev 1000 'ptop
'set t 1'
'set vpage 2.70 7.80 0.3 4.0'
'set zlog on'
'set ylevs 1000 700 500 300 200 100 70 50 30 20 10 7 5 3 2 1 0.4 0.2 0.1 0.04 0.02 .01 .004 .002 .001'
'set grads off'
'set clopts -1 -1 .13'
'set xlopts 1 4 .2'
'set ylopts 1 4 .2'
'set gxout shaded'
'set clevs 0'
'set ccols 84 0'
'd qz'
'set gxout contour'
'set ccolor 1'
'set clevs 'cvals
'd qz'
'set strsiz .15'
'set string 1 c'
'draw ylab Pressure (mb)'


'set vpage off'
'set string 1 c 6'
'set strsiz .13'
'draw string 5.5 8.3 'title'  ('season')'

'set strsiz .10'
'draw string 2.95  7.700 EXPID: 'expid'  'qdesc'  ('nmod')'
'draw string 7.95  7.700 'odesc'  ('nobs')'
'draw string 5.45  3.900 Difference (Left-Right)'

                date = getdate (begdate)
bmnthm = subwrd(date,1)
byearm = subwrd(date,2)
                date = getdate (enddate)
emnthm = subwrd(date,1)
eyearm = subwrd(date,2)
                date = getdate (begdateo)
bmntho = subwrd(date,1)
byearo = subwrd(date,2)
                date = getdate (enddateo)
emntho = subwrd(date,1)
eyearo = subwrd(date,2)

'set string 1 l 4'
'set strsiz .08'
'draw string 0.85  4.15 Mod Beg: 'bmnthm' 'byearm
'draw string 0.85  4.00 Mod End: 'emnthm' 'eyearm

'set string 1 r 4'
'set strsiz .08'
'draw string 10.15 4.15 Obs Beg: 'bmntho' 'byearo
'draw string 10.15 4.00 Obs End: 'emntho' 'eyearo

'set string 1 c 6'
'myprint -name 'output'/'oname'.'season

return

function getdate (date,month,year)
       num = 1
       bit = substr(date,num,1)
while( bit != '' )
       num = num+1
       bit = substr(date,num,1)
endwhile
       loc = num-7
     month = substr(date,loc  ,3)
      year = substr(date,loc+3,4)
return month' 'year
