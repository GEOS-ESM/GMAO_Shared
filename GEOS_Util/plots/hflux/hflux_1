* Sensible Heat Flux over Oceans
* ------------------------------
set vpage off
set parea off
set grid  off
set frame on
set xlopts 1 3 .18
set ylopts 1 3 .18

set dfile 1
set lat -90 90
set lon 0 360
set t 1

define mod = maskout( hflux&1,omaskmod )

set dfile 2
set lon 0 360
set lat -90 90
define obs1 = regrid2(hfluxo1&1,1,1,bs_p1,0,-90)
set dfile 3
set lon 0 360
set lat -90 90
define obs2 = regrid2(hfluxo2&1,1,1,bs_p1,0,-90)
define obs  = (obs1+obs2)/2

set rgb 50  20   50   254
set rgb 51  50  100   254
set rgb 52  75  140   240
set rgb 53 116  180   228
set rgb 54 120  243   230
set rgb 55 160  242   200
set rgb 56 200  240   180
set rgb 57 220  240   165
set rgb 58 240  240   150
set rgb 59 240  195   165
set rgb 60 240  150   180
set rgb 61 245  100   140
set rgb 62 245   50   100
set rgb 63 255    0    50

vpage 1 1 2 3
set clevs   0   2  4   6   8   10  12   15  18  20  30   50
set ccols 50 51  52  53  54  55  56   57  58  59  60  61   62
d mod
cbarn
draw title &0 (Over Oceans)

vpage 1 2 2 3
set clevs   0   2  4   6   8   10  12   15  18  20  30   50
set ccols 50 51  52  53  54  55  56   57  58  59  60  61   62
d obs
cbarn
draw title Observations (GSSTF and COADS)


* Sensible Heat Flux over Oceans (Difference)
* -------------------------------------------
define obsg = regrid2( obs,1,1,bs_p1,0,-90)
define modg = regrid2( mod,1,1,bs_p1,0,-90)

rgbset

vpage 1 3 2 3
set clevs -45 -40 -35 -30 -25 -20 -15 -10 -5 5 10 15 20 25 30 35 40 45
set ccols 49  48  47  46  45  44  43  42  41 0  61  62  63  64  65  66  67  68  69
d modg-obsg
cbarn
draw title Difference (Model-Obs)


* Zonal Means over Ocean
* ----------------------
set grid on
set dfile 2
set lon 0
define obs1z = ave( hfluxo1&1,lon=0,lon=360,-b )
set dfile 3
set lon 0
define obs2z = ave( hfluxo2&1,lon=0,lon=360,-b )

set dfile 1
set lon 0
define mod1z = ave( mod,lon=0,lon=360,-b )

set vpage 3.75 8.25 3.5 7
set grads off
set axlim -20 100
set cmark 0
set cstyle 1
set cthick 6
set ccolor 1
d obs1z
set cmark 0
set cstyle 3
set cthick 6
set ccolor 1
d obs2z
set cmark 0
set cstyle 1
set cthick 6
set ccolor 4
d mod1z
draw title Zonal Mean (Over Ocean)
draw ylab (W/m`a2`n)



* Sensible Heat Flux over Land
* ----------------------------
set grid off
set dfile 1
set lat -90 90
set lon 0 360
set t 1
define mod = maskout( hflux&1,lmaskmod )

vpage 2 1 2 3
set clevs -50 -40 -30 -20 -10 10  30 60 80 100 120 140 160
set ccols 49 47  45  43  41  0  61 62 63 64  65  66  67 69
d mod
cbarn
draw title &0 (Over Land)


* Zonal Means over Land
* ---------------------
set grid on
set lon 0
define mod1z = ave( mod,lon=0,lon=360,-b )

set vpage 3.75 8.25 0 3.5
set grads off
set axlim -20 100
set cmark 0
set cstyle 1
set cthick 6
set ccolor 4
d mod1z
draw title Zonal Mean (Over Land)
draw ylab (W/m`a2`n)

set vpage off
set string 1 c 6
set strsiz .15
draw string 4.46369 10.7421 GMAO Model Development
set strsiz .13
draw string 4.46369 10.4421 Sensible Heat Flux (W/m`a2`n)  &1
lines "&3/plots/hflux/hflux.stk" 1

myprint -name &2/hflux.&1
c
