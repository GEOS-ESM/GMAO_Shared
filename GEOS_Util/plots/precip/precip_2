function precip2 (args)
season  = subwrd(args,1)
expdsc  = subwrd(args,2)

'define CON = precon'season'geos'
'define LSP = preacc'season'geos-precon'season'geos'
'define DIF = LSP-CON'

'rgbset'
* Top Panel
* ---------
'set vpage 0 8.5 0.0 11'
'set parea 1.5 7.0 7.70 10.50'
'set mproj scaled'
'set frame on'

'set grads off'
'set xlopts 1 3 .11'
'set ylopts 1 3 .11'

'set gxout shaded'
'set grads off'
'set clevs .2 .4  .6  .8  1.0  1.2 1.4  1.6  1.8 2.0 2.3 2.6 3.0 3.3 3.6 4.0 4.5 5.0 5.5 6.0 7.0 8.0 9.0 10. 11. 12. 15.'
'set ccols 0  50   42   43   44   45   46   47   48   49   39   38   37   36   35  34  33  32  31  21  22  23  24  25   26   27   28   29'
'd CON'
'set parea 0 8.5 7.0 11'
'cbarn -vert'
'set parea off'

* Middle Panel
* ------------
'set vpage 0 8.5 0.0 11'
'set parea 1.5 7.0 4.30 7.10'
'set mproj scaled'

'set grads off'
'set gxout shaded'
'set clevs .2 .4  .6  .8  1.0  1.2 1.4  1.6  1.8 2.0 2.3 2.6 3.0 3.3 3.6 4.0 4.5 5.0 5.5 6.0 7.0 8.0 9.0 10. 11. 12. 15.'
'set ccols 0  50   42   43   44   45   46   47   48   49   39   38   37   36   35  34  33  32  31  21  22  23  24  25   26   27   28   29'
'd LSP'

* Bottom Panel
* ------------
'set vpage 0 8.5 0.0 11'
'set parea 1.5 7.0 0.90 3.70'
'set grads off'

'set clevs -8 -7 -6 -5 -4 -3 -2 -1 -0.5 0.5 1 2 3 4 5 6 7 8'
'set ccols 55  49  47  45  44  36  34  33  32  0  21  22  23  24  25  26  27  28 69'
'd DIF'
'cbarn -snum 0.55'


* STATS
* -----
'stats CON'
 avgCON = subwrd(result,1)
 stdCON = subwrd(result,2)
'stats LSP'
 avgLSP = subwrd(result,1)
 stdLSP = subwrd(result,2)
'stats DIF'
 avgDIF = subwrd(result,1)
 stdDIF = subwrd(result,2)


'set vpage off'
'set parea off'
'set mproj latlon'
'set string 1 c 4'
'set strsiz .08'

'draw string 0.52 10  Mean: 'avgCON
'draw string 0.52 9.8  Std: 'stdCON
'draw string 0.52 6.6 Mean: 'avgLSP
'draw string 0.52 6.4  Std: 'stdLSP
'draw string 0.52 3.2 Mean: 'avgDIF
'draw string 0.52 3.0  Std: 'stdDIF

'set string 1 c 6'
'set strsiz .11'
'draw string 4.25 10.9 'expdsc'  ('season')'

'draw string 4.25 10.64 Convective + Anvil Precipitation (mm/day)'
'draw string 4.25 7.23 Large-Scale Precipitation (mm/day)'
'draw string 4.25 3.82 Large-Scale minus Convective  (mm/day)'

'print'
