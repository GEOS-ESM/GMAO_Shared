#!/bin/csh -f
alias MATH 'set \!:1 = `echo "\!:3-$" | bc -l`'

@ nmax = $#argv

# Set Variables
# -------------
set        bw = false
set antialias = false
set rotate  = 0
set season  = ""
set output  = "."
set density = "90x90"
MATH asprat = 0.7533
MATH   xdim = 1200
MATH   ydim = xdim * asprat

@       n  = 1
while( $n <= $nmax )
       if( "$argv[$n]" == "-name" ) then
                    @ n = $n + 1
             set name   = $argv[$n]
       endif
       if( "$argv[$n]" == "-season"   ) then
                    @ n = $n + 1
             set season = $argv[$n]
       endif
       if( "$argv[$n]" == "-output"   ) then
                    @ n = $n + 1
             set output = $argv[$n]
       endif
       if( "$argv[$n]" == "-rotate"   ) then
                    @ n = $n + 1
             set rotate = $argv[$n]
       endif
       if( "$argv[$n]" == "-density"  ) then
                    @ n = $n + 1
            set density = $argv[$n]
       endif
       if( "$argv[$n]" == "-xdim"  ) then
                    @ n = $n + 1
               set xdim = $argv[$n]
       endif
       if( "$argv[$n]" == "-ydim"  ) then
                    @ n = $n + 1
               set ydim = $argv[$n]
       endif
       if( "$argv[$n]" == "-ext"  ) then
                    @ n = $n + 1
                set ext = $argv[$n]
       endif
       if( "$argv[$n]" == "-bw"        ) set        bw = true
       if( "$argv[$n]" == "-antialias" ) set antialias = true
     @ n = $n + 1
end

# Landscape
# ---------
if( $rotate == 90  ) then
     set dim1 = $xdim
     set dim2 = $ydim
endif

# Portrait
# --------
if( $rotate == 0  ) then
     set dim1 = $ydim
     set dim2 = $xdim
endif

                     set file = ${name}
if( .$season != .  ) set file = ${file}.${season}
if( .$output != .. ) set file = ${output}/${file}


# Create Postscript from Grads
# ----------------------------
        if( -e $file.out ) then
             if( $rotate == 0   ) $GXYAT  -y $dim1 -x $dim2 -i $file.out -o $file.$ext
             if( $rotate == 90  ) $GXYAT  -x $dim1 -y $dim2 -i $file.out -o $file.$ext
        endif

# Create GIF from Postscript
# --------------------------
        if( -e $file.ps ) then
           if( $antialias == 'false' )  convert            -seed 1 -rotate $rotate -density $density $file.ps $file.$ext
           if( $antialias == 'true'  )  convert +antialias -seed 1 -rotate $rotate -density $density $file.ps $file.$ext
        endif

# Remove Intermediate Files
# -------------------------
        if( -e $file.$ext ) then
               if( -e $file.out ) /bin/rm $file.out
               if( -e $file.ps  ) /bin/rm $file.ps
               chmod 644 $file.$ext
               echo Finished Creating:  $file.$ext
        endif

