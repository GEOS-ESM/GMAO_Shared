function vpage (args)

*************************************************************
*****                                                   *****
*****  Usage:  vpage xloc yloc xmax ymax                *****
*****                                                   *****
*************************************************************

'numargs  'args
 numargs = result

xloc = subwrd(args,1)
yloc = subwrd(args,2)
xmax = subwrd(args,3)
ymax = subwrd(args,4)

  top = 0.5
  bot = 0.2
 left = 0.2
right = 0.2

  top = 0.0
  bot = 0.0
 left = 0.0
right = 0.0

scalex = 1
scaley = 1

       num = 0
while( num < numargs )
       num = num + 1
if( subwrd(args,num)='-scalex' ) ; scalex = subwrd(args,num+1) ; endif
if( subwrd(args,num)='-scaley' ) ; scaley = subwrd(args,num+1) ; endif
if( subwrd(args,num)='-top'    ) ; top    = subwrd(args,num+1) ; endif
if( subwrd(args,num)='-bot'    ) ; bot    = subwrd(args,num+1) ; endif
if( subwrd(args,num)='-left'   ) ; left   = subwrd(args,num+1) ; endif
if( subwrd(args,num)='-right'  ) ; right  = subwrd(args,num+1) ; endif
endwhile

'set vpage off'
'getinfo page'

pagex = subwrd(result,1)
pagey = subwrd(result,2)

dx0 = ( pagex / xmax )
dy0 = ( pagey / ymax )

dx  = ( pagex / xmax ) - ( left + right )
dy  = ( pagey / ymax ) - ( top  + bot   )

      x1 = (xloc-1)*dx0 + left
      x2 =  x1 + dx - 1e-3

       y2 = pagey - (yloc-1)*dy0 - top
       y1 = y2 - dy - 1e-3

say 'Default page sizex: 'pagex
say 'Default page sizey: 'pagey
say 'Offsets top: 'top'  bot: 'bot'  left: 'left'  right: 'right

if( x1 < 0 )
    x1 = left
endif
if( y1 < 0 )
    y1 = bot
endif
if( x2 > pagex-right )
    x2 = pagex-right
endif
if( y2 > pagey-top )
    y2 = pagey-top
endif

say 'set vpage 'x1' 'x2' 'y1' 'y2
'set  vpage 'x1' 'x2' 'y1' 'y2
'set  grads off'

pagex = x2-x1
pagey = y2-y1
'run setenv PAGEX 'pagex
'run setenv PAGEY 'pagey

*'getinfo page'
*pagex = subwrd(result,1)
*pagey = subwrd(result,2)

say 'Final page sizex: 'pagex
say 'Final page sizey: 'pagey

xmid   = ( x1 + x2 )/2
ymid   = ( y1 + y2 )/2
xleft  =   x1
xright =   x2
ybot   =   y1
ytop   =   y2

say ' '
say 'xmid: 'xmid'  ybot: 'ybot'  ytop: 'ytop'  ymid: 'ymid'  xleft: 'xleft'  xright: 'xright
say ' '

return xmid' 'ybot' 'ytop' 'ymid' 'xleft' 'xright

