function keg (args)

expid   = subwrd(args,1)
expfile = subwrd(args,2)
expdsc  = subwrd(args,3)
obsnam  = subwrd(args,4)
obsdsc  = subwrd(args,5)
obsfile = subwrd(args,6)
output  = subwrd(args,7)

'run getenv "GEOSUTIL"'
         geosutil = result

'getinfo tmin'
         tmin = result
'getinfo tmax'
         tmax = result

'set t 'tmin
'getinfo year'
         year = result
'set time JAN'year' DEC'year

'getinfo tmin'
         tmin = result
'getinfo tmax'
         tmax = result

         yrp1 = year + 1
         yrm1 = year - 1
'set time DEC'yrm1' FEB'yrp1

'run getenv "RADLWAVE"'
'define radlwave = 'result
'run getenv "RADSWAVE"'
'define radswave = 'result

'run getenv "ORADLWAVE"'
'define oradlwave = 'result
'run getenv "ORADSWAVE"'
'define oradswave = 'result

'set dfile 'expfile
'set x 1'
'set y 1'
'set z 1'
'define net = -( radlwgclim- radswgclim)'

'run minmax radlwgclim'
       radmax = subwrd(result,1)
       radmin = subwrd(result,2)

'run minmax radswgclim'
       dummax = subwrd(result,1)
       dummin = subwrd(result,2)
   if( dummax > radmax ) ; radmax = dummax ; endif
   if( dummin < radmin ) ; radmin = dummin ; endif

'set dfile 'obsfile
'set x 1'
'set y 1'
'set z 1'
'define onet = -(oradlwgclim-oradswgclim)'

'run minmax oradlwgclim'
       dummax = subwrd(result,1)
       dummin = subwrd(result,2)
   if( dummax > radmax ) ; radmax = dummax ; endif
   if( dummin < radmin ) ; radmin = dummin ; endif

'run minmax oradswgclim'
       dummax = subwrd(result,1)
       dummin = subwrd(result,2)
   if( dummax > radmax ) ; radmax = dummax ; endif
   if( dummin < radmin ) ; radmin = dummin ; endif

radave = (radmax+radmin)/2
raddif = 1.1*(radmax-radave)

radmax = radave + raddif
radmin = radave - raddif

* Plot Global Mean Values
* -----------------------
'set vpage 0 11 3.5 8'
'set grads off'
'set ylab %.0f'
'set axlim 'radmin' 'radmax

'set dfile 'expfile
'set x 1'
'set y 1'
'set z 1'
'set cmark  0'
'set cstyle 1'
'set ccolor 2'
'set cthick 3'
'd radlwgclim'
'set cmark  0'
'set cstyle 1'
'set ccolor 4'
'set cthick 3'
'd radswgclim'

'set cmark  0'
'set cstyle 1'
'set ccolor 2'
'set cthick 10'
'd radlwave'
'set cmark  0'
'set cstyle 1'
'set ccolor 4'
'set cthick 10'
'd radswave'

'set dfile 'obsfile
'set x 1'
'set y 1'
'set z 1'
'set cmark  0'
'set cstyle 3'
'set ccolor 2'
'set cthick 3'
'd oradlwgclim'
'set cmark  0'
'set cstyle 3'
'set ccolor 4'
'set cthick 3'
'd oradswgclim'

'set cmark  0'
'set cstyle 3'
'set ccolor 2'
'set cthick 10'
'd oradlwave'
'set cmark  0'
'set cstyle 3'
'set ccolor 4'
'set cthick 10'
'd oradswave'


* Plot Global Mean Time-Average Values
* ------------------------------------
'set t 'tmin
'getinfo date'
         date = result

'd  radlwave'
    radlwave = subwrd(result,4)
'd  radswave'
    radswave = subwrd(result,4)
'd oradlwave'
   oradlwave = subwrd(result,4)
'd oradswave'
   oradswave = subwrd(result,4)

'q w2xy 'date' 'radlwave
                ymlwg = subwrd(result,6)
                x.1   = radlwave
                y.1   = ymlwg
                c.1   = 2
                L.1   = "(Solid)"
'q w2xy 'date' 'radswave
                ymswg = subwrd(result,6)
                x.2   = radswave
                y.2   = ymswg
                c.2   = 4
                L.2   = "(Solid)"
'q w2xy 'date' 'oradlwave
                yolwg = subwrd(result,6)
                x.3   = oradlwave
                y.3   = yolwg
                c.3   = 2
                L.3   = "(Dash)"
'q w2xy 'date' 'oradswave
                yoswg = subwrd(result,6)
                x.4   = oradswave
                y.4   = yoswg
                c.4   = 4
                L.4   = "(Dash)"

say 'ymlwg: 'ymlwg' 'L.1
say 'yolwg: 'yolwg' 'L.3
say 'ymswg: 'ymswg' 'L.2
say 'yoswg: 'yoswg' 'L.4

* Sort Mean End-Point Values
* --------------------------
m = 1
while( m<=4 )
  n = m+1
  while( n<=4 )
  if( y.n < y.m )
      dum = x.m ; x.m = x.n ; x.n = dum
      dum = y.m ; y.m = y.n ; y.n = dum
      dum = c.m ; c.m = c.n ; c.n = dum
      dum = L.m ; L.m = L.n ; L.n = dum
  endif
  n = n+1
  endwhile
m = m+1
endwhile

m = 1
say 'm: 'm' y: 'y.m' L: 'L.m
m = m+1
while( m<=4 )
k = m-1
if( y.m - y.k < 0.1 )
    y.m = y.k + 0.1
endif
say 'm: 'm' y: 'y.m' L: 'L.m
m = m+1
endwhile

'set strsiz .08'

'set string 'c.1' r 5'
'draw string 1.4 'y.1' 'x.1' 'L.1
'set string 'c.2' r 5'
'draw string 1.4 'y.2' 'x.2' 'L.2

'set string 'c.3' r 5'
'draw string 1.4 'y.3' 'x.3' 'L.3
'set string 'c.4' r 5'
'draw string 1.4 'y.4' 'x.4' 'L.4



* Plot NET
* --------
'set time DEC'yrm1' FEB'yrp1

'run minmax net'
       netmax = subwrd(result,1)
       netmin = subwrd(result,2)

'run minmax onet'
       dummax = subwrd(result,1)
       dummin = subwrd(result,2)
   if( dummax > netmax ) ; netmax = dummax ; endif
   if( dummin < netmin ) ; netmin = dummin ; endif

     netmax = netmax * 1.1
     netmin = netmin * 1.1

'set vpage 0 11 0 4.5'
'set grads off'
'set axlim 'netmin' 'netmax
'set cmark  0'
'set cstyle 1'
'set ccolor 3'
'set cthick 3'
'd net'
'set cmark  0'
'set cstyle 3'
'set ccolor 3'
'set cthick 3'
'd onet'
'define radave = radswave-radlwave'
'set cmark  0'
'set cstyle 1'
'set ccolor 3'
'set cthick 10'
'd radave'
'define oradave = oradswave-oradlwave'
'set cmark  0'
'set cstyle 3'
'set ccolor 3'
'set cthick 10'
'd oradave'

'set t 'tmin
'getinfo date'
         date = result
'd radave'
   radave = subwrd(result,4)
'q w2xy 'date' 'radave
    ymrad = subwrd(result,6)
'd oradave'
   oradave = subwrd(result,4)
'q w2xy 'date' 'oradave
    yorad = subwrd(result,6)

'set strsiz .08'

'set string 3 r 5'
'draw string 1.4 'ymrad ' 'radave' (Solid)'
'set string 3 r 5'
'draw string 1.4 'yorad ' 'oradave' (Dash)'




'set vpage off'
'set string 1 c 6'
'set strsiz .14'
'draw string 6.15 8.35 'expid' (Solid)  vs.  'obsnam' (Dash)'
'draw string 6.15 7.80 Globally Averaged TOA Radiation Budget (W/m`a2`n)'
'set strsiz .12'
'draw string 6.15 8.12 'expdsc

'set string 1 l 6'
'set line 2 1 6'
'draw line   2.0 7.5 2.5 7.5'
'draw string 2.6 7.5 Outgoing LW Rad'
'set line 4 1 6'
'draw line   4.6 7.5 5.1 7.5'
'draw string 5.2 7.5 Net Downward SW Rad'
'set line 3 1 6'
'draw line   7.9 7.5 8.4 7.5'
'draw string 8.5 7.5 Net Downward Rad'

'myprint -name 'output'/RAD_TIME_SERIES_CLIM.'obsnam
return
